{
  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-24.05";
  };

  outputs = { self, nixpkgs }:
    let
      inherit (nixpkgs.lib) genAttrs getName;

      forAllSystems = genAttrs [ "x86_64-linux" "aarch64-linux" "aarch64-darwin" ];
      forAllPkgs = function: forAllSystems (system: function pkgs.${system});

      pkgs = forAllSystems (system: (import nixpkgs {
        inherit system;
        config.allowUnfreePredicate =
          pkg: builtins.elem (getName pkg) [ "rider" ];
        overlays = [ ];
      }));
    in
    {
      formatter = forAllPkgs (pkgs: pkgs.nixpkgs-fmt);

      devShells = forAllPkgs (pkgs:
        with pkgs.lib;
        let
          rider-env = pkgs.jetbrains.rider.overrideAttrs (old: {
            nativeBuildInputs = old.nativeBuildInputs ++ (with pkgs; [
              dotnetCorePackages.sdk_9_0
              msbuild
            ]);
          });
          
          start-rider-env = pkgs.writeShellScriptBin "start-rider" ''nohup rider "$@" > /dev/null &'';
        in
        {
          default = pkgs.mkShell rec {
            nativeBuildInputs = with pkgs; [
              dotnetCorePackages.sdk_9_0
              
              rider-env
              start-rider-env
            ];

            buildInputs = with pkgs; [
              # openssl
            ];

            LD_LIBRARY_PATH = makeLibraryPath buildInputs;
          };
        });
    };
}
